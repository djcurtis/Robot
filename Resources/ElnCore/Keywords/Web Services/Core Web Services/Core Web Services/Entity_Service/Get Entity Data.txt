*** Settings ***
Documentation     The following test cases are all for the endpoint: GET /services/1.0/entities/{entityId}/data
Suite Setup       Record Service Suite Setup
Suite Teardown    Record Service Suite Teardown
Force Tags        Get Entity Data
# Library           EntityAPILibrary
# Library           SecurityAPILibrary
Library           XMLLibrary
Resource          ../../../Libraries/common_resource.txt
Resource          record_service_resources.txt    # suite setup and teardown
Resource          ../../../Libraries/Web Services/REST_SecurityService/rest_security_service_resource.txt
Resource          ../../../Libraries/General Setup/general setup tools.txt
Resource          ../../../Libraries/Core Web Services/Record Service/rest_record_service_resource.txt    # REST Record Service
Resource          ../../../Libraries/Core Web Services/Entity Service/rest_entity_service_resource.txt    # REST Entity Service
Resource          ../../../Libraries/Core Web Services/Entity Lock Service/rest_entity_lock_service.txt

*** Test Cases ***
Get entity data
    [Documentation]    A successful call returns the HTTP status 200 and a stream of bytes representing the entity data
    ${experiment_name}=    Set Variable    GED-S001
    ${experiment_id}=    Create Experiment    ${project_id}    ${experiment_name}
    ${document_id}=    Add Text Entity    ${experiment_id}
    ${document_version_id}    Get Entity Version ID    ${document_id}
    ${entity_data}=    Get Entity Data    ${document_id}    ${document_version_id}    200
    # Checking expectations
    Should Contain    ${entity_data}    Test text data

Get entity data with wrong entity version id
    [Documentation]    If the call parameter ‘entityVersionId’ is invalid, then the HTTP status code 404 is returned to the client
    ${experiment_name}=    Set Variable    GED-S002
    ${experiment_id}=    Create Experiment    ${project_id}    ${experiment_name}
    ${document_id}=    Add Text Entity    ${experiment_id}
    ${document_version_id}    Get Entity Version ID    ${document_id}
    # wrong entity version id
    ${entity_data}=    Get Entity Data    ${document_id}    WrongEntityVersionId    404

Get entity data without permissions
    [Documentation]    /
    ...
    ...    A client requires the OPEN_<CONTAINER_ ENTITY_TYPE> permission. If it does not have it, then the HTTP status code 403 is returned to the client
    ...
    ...    A client requires the EDIT_<CONTAINER_ ENTITY_TYPE> permission. If it does not have it, then the HTTP status code 403 is returned to the client
    ${experiment_name}=    Set Variable    GED-S003
    ${experiment_id}=    Create Experiment    ${project_id}    ${experiment_name}
    ${document_id}=    Add Text Entity    ${experiment_id}
    ${document_version_id}    Get Entity Version ID    ${document_id}
    # wrong entity version id
    Use User With Permissions    ${username}    ${password}    ${experiment_id}
    ${entity_data}=    Get Entity Data    ${document_id}    ${document_version_id}    403

Get entity data with not existing entity
    [Documentation]    If the entity does not exist, then the HTTP status code 404 is returned to the client
    ${experiment_name}=    Set Variable    GED-S005
    ${experiment_id}=    Create Experiment    ${project_id}    ${experiment_name}
    ${document_id}=    Add Text Entity    ${experiment_id}
    ${document_version_id}    Get Entity Version ID    ${document_id}
    # Not existing entity
    ${entity_data}=    Get Entity Data    WrongEntityId    WrongEntityVersonId    404

*** Keywords ***
Add Text Entity
    [Arguments]    ${parent_entity_id}    ${text_data}=Test text data
    [Documentation]    Add a Text entity and save record as version.
    ...
    ...    *Arguments*
    ...
    ...    _parent_entity_id_ - the parent entity id
    ...
    ...    _text_data_ - the text data. Default set to "Test text data"
    ...
    ...    *Return value*
    ...
    ...    The Text entity id
    ...
    ...    *Precondition*
    ...    None
    ...
    ...    *Example*
    Rest_Entity_Lock_Service.Lock Entity    ${experiment_id}
    ${text_entity_id}=    Add DOCUMENT With String Data    ${experiment_id}    ${text_data}    text/html    HTML_TEXT    Uploaded Document
    ...    .html
    ${document_version_id}=    Get Entity Version ID    ${text_entity_id}
    Commit Versioned Entity    ${experiment_id}
    Rest_Entity_Lock_Service.Unlock Entity    ${experiment_id}
    [Return]    ${text_entity_id}    # The newly created entity id
