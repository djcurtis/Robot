*** Settings ***
Documentation     The following test cases are all for the endpoint: /services/1.0/records/{entityId}
Suite Setup       Record Service Suite Setup
Suite Teardown    Record Service Suite Teardown
Force Tags        EWB-SAPI-SSP-ENT-GAT    Get Attributes
# Library           EntityAPILibrary
# Library           SecurityAPILibrary
Library           XMLLibrary
Resource          ../../../Libraries/common_resource.txt
Resource          record_service_resources.txt    # suite setup and teardown
Resource          ../../../Libraries/Web Services/REST_SecurityService/rest_security_service_resource.txt
Resource          ../../../Libraries/General Setup/general setup tools.txt
Resource          ../../../Libraries/Core Web Services/Record Service/rest_record_service_resource.txt    # REST Record Service
Resource          ../../../Libraries/Core Web Services/Entity Service/rest_entity_service_resource.txt    # REST Entity Service

*** Test Cases ***
Get attributes
    [Documentation]    EWB-SAPI-SSP-ENT-GAT-S001
    ...
    ...    A successful call returns the HTTP status 200 and an instance of SAPIAttributeSequence
    [Tags]    EWB-SAPI-SSP-ENT-GAT-S001
    ${experiment_name}=    Set Variable    GAT-S001
    ${experiment_id}=    Create Experiment    ${project_id}    ${experiment_name}
    ${experiment_version_id}=    Get Entity Version ID    ${experiment_id}
    ${sapi_attribute_sequence}=    Get Attributes    ${experiment_id}    ${experiment_version_id}
    # Checking expectations
    Should Contain    ${sapi_attribute_sequence}    GAT-S001

Get attributes with missing entity version id
    [Documentation]    EWB-SAPI-SSP-ENT-GAT-S002
    ...
    ...    If the call parameter ‘entityVersionId’ is missing or invalid, then the HTTP status code 400 is returned to the client
    [Tags]    EWB-SAPI-SSP-ENT-GAT-S002
    ${experiment_name}=    Set Variable    GAT-S002
    ${experiment_id}=    Create Experiment    ${project_id}    ${experiment_name}
    # missing entity version id
    ${attributes}=    Get Attributes    ${experiment_id}    ${EMPTY}    400
    # wrong entity version id
    ${attributes}=    Get Attributes    ${experiment_id}    WrongEntityVersionId    404

Get attributes with not existing entity
    [Documentation]    EWB-SAPI-SSP-RES-GEN-S003
    ...
    ...    If the entity does not exist, then the HTTP status code 404 is returned to the client
    [Tags]    EWB-SAPI-SSP-ENT-GAT-S003
    ${experiment_name}=    Set Variable    GAT-S003
    ${experiment_id}=    Create Experiment    ${project_id}    ${experiment_name}
    ${experiment_version_id}=    Get Entity Version ID    ${experiment_id}
    ${attributes}=    Get Attributes    WrongId    WrongVersionId    404

Get attributes without permissions
    [Documentation]    EWB-SAPI-SSP-ENT-GAT-S004
    ...
    ...    A client requires the OPEN_< ENTITY_TYPE> permission. If it does not have it, then the HTTP status code 403 is returned to the client
    [Tags]    EWB-SAPI-SSP-ENT-GAT-S004
    ${experiment_name}=    Set Variable    GAT-S004
    ${experiment_id}=    Create Experiment    ${project_id}    ${experiment_name}
    ${experiment_version_id}=    Get Entity Version ID    ${experiment_id}
    Use User With Permissions    ${username}    ${password}    ${experiment_id}
    ${attributes}=    Get Attributes    ${experiment_id}    ${experiment_version_id}    403

*** Keywords ***
