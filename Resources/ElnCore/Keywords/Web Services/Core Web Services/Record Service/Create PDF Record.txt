*** Settings ***
Documentation     The following tests are for the endpoint: /services/1.0/records/{entityId}/pdf
Suite Setup       Record Service Suite Setup
Suite Teardown    Record Service Suite Teardown
Force Tags        Record Service
Resource          ../../../Libraries/common_resource.txt
Resource          ../../../Libraries/General Setup/general setup tools.txt
Resource          ../../../Libraries/Core Web Services/Entity Service/rest_entity_service_resource.txt
Resource          ../../../Libraries/Core Web Services/Record Service/rest_record_service_resource.txt
Resource          ../../../Libraries/Core Web Services/Entity Lock Service/rest_entity_lock_service.txt
Resource          ../../../Libraries/Web Services/REST_SecurityService/rest_security_service_resource.txt
Resource          record_service_common_resource.txt    # common record service setup and teardown
Library           IDBSHttpLibrary
# Library           EntityAPILibrary

*** Test Cases ***
Check Can Create PDF
    [Documentation]    A successful call returns the HTTP status 200 and the entity id of the created PDF. The record PDF is added to the record PDF folder
    ${experiment_name}=    Set Variable    CRP-S001
    ${experiment_id}=    Create Publishable Experiment    ${experiment_name}    ${project_id}
    ${entity_version_id}=    Get Entity Version ID    ${experiment_id}
    ${lock_response}=    EntityAPILibrary.Lock Entity    ${experiment_id}
    ${pdf_id}=    Create Record PDF    ${experiment_id}    ${entity_version_id}
    ${lock_response}=    EntityAPILibrary.Unlock Entity    ${experiment_id}

Check Can Create PDF If No Version Is Specified
    [Documentation]    If the call parameter ‘entityVersionId’ is missing, then the latest entity version is used
    ${experiment_name}=    Set Variable    CRP-S002
    ${experiment_id}=    Create Publishable Experiment    ${experiment_name}    ${project_id}
    ${lock_response}=    EntityAPILibrary.Lock Entity    ${experiment_id}
    ${pdf_id}=    Create Record PDF    ${experiment_id}    ${EMPTY}
    ${lock_response}=    EntityAPILibrary.Unlock Entity    ${experiment_id}

Check Create PDF Fails If Entity Does Not Exist
    [Documentation]    If the entity does not exist, then the HTTP status code 404 is returned to the client
    ${pdf_id}=    Create Record PDF    EWB-SAPI-SSP-RES-CRP-S003    ${EMPTY}    404
    Response Body Should Contain    Resource not found for given ID: Requested entity does not exist

Check Create PDF Fails If Not of Type Record
    [Documentation]    If the entity is not of type record, then the HTTP status code 400 is returned to the client
    ${template_name}=    Set Variable    CRP-S004
    ${template_id}=    EntityAPILibrary.Create Template    ${project_id}    ${template_name}
    ${lock_response}=    EntityAPILibrary.Lock Entity    ${project_id}
    ${pdf_id}=    Create Record PDF    ${project_id}    ${EMPTY}    400
    Response Body Should Contain    PDFs can only be generated by record type entities
    ${lock_response}=    EntityAPILibrary.Unlock Entity    ${project_id}

Check Can Create PDF With Minimum Permissions
    [Documentation]    A client requires the OPEN_<ENTITY_TYPE> permission. If it does not have it, then the HTTP status code 403 is returned to the client
    ${name}=    Set Variable    CUSTOM_CRP-S005
    ${role_name}=    Create Custom PDF Role    ${name}
    ${experiment_id}=    Create Publishable Experiment    CRP-S005    ${project_id}
    ${response}=    Assign User Entity Role    ${LIMITED_PERMISSIONS_USERNAME}    ${role_name}    ${experiment_id}
    Set Test Variable    ${SERVICES USERNAME}    ${LIMITED_PERMISSIONS_USERNAME}
    Set Test Variable    ${SERVICES PASSWORD}    ${PASSWORD}
    ${lock_response}=    EntityAPILibrary.Lock Entity    ${experiment_id}
    ${entity_version_id}=    Get Entity Version ID    ${experiment_id}
    ${pdf_id}=    Create Record PDF    ${experiment_id}    ${entity_version_id}
    ${lock_response}=    EntityAPILibrary.Unlock Entity    ${experiment_id}

Check Cannot Create PDF Without OPEN_<ENTITY_TYPE> permission
    [Documentation]    A client requires the OPEN_<ENTITY_TYPE> \ OR EDIT_<ENTITY_TYPE> permissions. If it does not have them, then the HTTP status code 403 is returned to the client
    ${name}=    Set Variable    CUSTOM_CRP-S005-min
    ${experiment_id}=    Create Publishable Experiment    CRP-S005-min    ${project_id}
    ${entity_version_id}=    Get Entity Version ID    ${experiment_id}
    ${role_name}=    Create Custom PDF Role    ${name}
    ${remove_permission}=    Remove Role Permission    ${role_name}    OPEN_EXPERIMENT
    ${remove_permission}=    Remove Role Permission    ${role_name}    EDIT_EXPERIMENT
    ${response}=    Assign User Entity Role    ${LIMITED_PERMISSIONS_USERNAME}    ${role_name}    ${experiment_id}
    Set Test Variable    ${SERVICES USERNAME}    ${LIMITED_PERMISSIONS_USERNAME}
    Set Test Variable    ${SERVICES PASSWORD}    ${PASSWORD}
    Log    Switching default user...    INFO
    ${lock_response}=    EntityAPILibrary.Lock Entity    ${experiment_id}
    ${pdf_id}=    Create Record PDF    ${experiment_id}    ${entity_version_id}    403
    Response Body Should Contain    Authorization failure: Need create PDF, PDF_FOLDER and either record OPEN or EDIT permissions
    ${lock_response}=    EntityAPILibrary.Unlock Entity    ${experiment_id}

Check Cannot Create PDF Without CREATE_PDF permission
    [Documentation]    A client requires the CREATE_PDF permission. If it does not have it, then the HTTP status code 403 is returned to the client
    ${name}=    Set Variable    CUSTOM_CRP-S007
    ${experiment_id}=    Create Publishable Experiment    CRP-S007    ${project_id}
    ${entity_version_id}=    Get Entity Version ID    ${experiment_id}
    ${role_name}=    Create Custom PDF Role    ${name}
    ${remove_permission}=    Remove Role Permission    ${role_name}    CREATE_PDF
    ${response}=    Assign User Entity Role    ${LIMITED_PERMISSIONS_USERNAME}    ${role_name}    ${experiment_id}
    Set Test Variable    ${SERVICES USERNAME}    ${LIMITED_PERMISSIONS_USERNAME}
    Set Test Variable    ${SERVICES PASSWORD}    ${PASSWORD}
    Log    Switching default user...    INFO
    ${lock_response}=    EntityAPILibrary.Lock Entity    ${experiment_id}
    ${pdf_id}=    Create Record PDF    ${experiment_id}    ${entity_version_id}    403
    Response Body Should Contain    Authorization failure: Need create PDF, PDF_FOLDER and either record OPEN or EDIT permissions
    ${lock_response}=    EntityAPILibrary.Unlock Entity    ${experiment_id}
