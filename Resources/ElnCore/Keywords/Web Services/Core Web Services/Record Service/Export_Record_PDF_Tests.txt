*** Settings ***
Documentation     The following tests are for the endpoint: \ /services/1.0/records/{entityId}/pdf
Suite Setup       Record Service Suite Setup
Suite Teardown    Record Service Suite Teardown
Force Tags        Record Service
Resource          ../../../Libraries/common_resource.txt
Resource          ../../../Libraries/General Setup/general setup tools.txt
Resource          ../../../Libraries/Core Web Services/Record Service/rest_record_service_resource.txt
Resource          ../../../Libraries/Web Services/REST_SecurityService/rest_security_service_resource.txt
Resource          record_service_common_resource.txt    # common record service setup and teardown
Resource          PDF content acceptance/test utilities.txt
Library           IDBSHttpLibrary
Resource          ../../../Libraries/Core Web Services/Entity Service/rest_entity_service_resource.txt

*** Test Cases ***
Check Whether the User Can Get Published Record
    [Documentation]    A successful call returns the HTTP status 200 and the created PDF bytes. No PDF entity is saved in the record PDF folder.
    ${experiment_name}=    Set Variable    GRP-S001
    ${experiment_id}=    Create Published Experiment PDF    ${experiment_name}    ${project_id}
    ${entity_version_id}=    Get Entity Version ID    ${experiment_id}
    ${pdf}=    Export Record To PDF    ${experiment_id}    ${entity_version_id}
    Assert Pdf As Expected    ${experiment_id}    ${expected_folder}\\${APP_SERVER_OS}\\${TEST NAME}.pdf    ${output_folder}\\${TEST NAME}.pdf

Check Get Published Record Fails for Bad Id
    [Documentation]    If the entity does not exist, then the HTTP status code 404 is returned to the client
    ${bad_id}=    Set Variable    GRP-S003
    ${response}=    Export Record To PDF    ${bad_id}    ${EMPTY}    404
    Response Body Should Contain    Resource not found for given ID: Requested entity does not exist

Check Get Published Record Fails If Not Of Type Record
    [Documentation]    If the entity does not exist, then the HTTP status code 404 is returned to the client
    ${bad_id}=    Set Variable    GRP-S004
    ${response}=    Export Record To PDF    ${project_id}    ${EMPTY}    400
    Response Body Should Contain    PDFs can only be generated by record type entities

Check Get Published Record Fails Without EDIT_EXPERIMENT Permission
    [Documentation]    A client requires the OPEN_<ENTITY_TYPE> \ _or_ \ EDIT_<ENTITY_TYPE> permission. If it does not have it, then the HTTP status code 403 is returned to the client
    ${experiment_id}=    Create Publishable Experiment    GRP-S005    ${project_id}
    ${name}=    Set Variable    CUSTOM_GRP_S005
    ${entity_version_id}=    Get Entity Version ID    ${experiment_id}
    ${role_name}=    Create Custom PDF Role    ${name}
    ${permission_response}=    Remove Role Permission    ${role_name}    EDIT_EXPERIMENT
    ${permission_response}=    Remove Role Permission    ${role_name}    OPEN_EXPERIMENT
    ${response}=    Assign User Entity Role    ${LIMITED_PERMISSIONS_USERNAME}    ${role_name}    ${experiment_id}
    Set Test Variable    ${SERVICES USERNAME}    ${LIMITED_PERMISSIONS_USERNAME}
    Set Test Variable    ${SERVICES PASSWORD}    ${PASSWORD}
    Log    As a new user...    INFO
    ${pdf_response}=    Export Record To PDF    ${experiment_id}    ${entity_version_id}    403
    Response Body Should Contain    Authorization failure: Need create PDF, PDF_FOLDER and either record OPEN or EDIT permissions
    ${unassign_role}=    Delete User Entity Role    ${LIMITED_PERMISSIONS_USERNAME}    ${role_name}    ${experiment_id}
    Run Keyword And Ignore Error    Delete Custom PDF Role    ${role_name}

Check User Can Get Published Record Without Version Id
    [Documentation]    A successful call returns the HTTP status 200 and the created PDF bytes. No PDF entity is saved in the record PDF folder.
    ${experiment_name}=    Set Variable    GRP-S002
    ${experiment_id}=    Create Published Experiment PDF    ${experiment_name}    ${project_id}
    ${pdf}=    Export Record To PDF    ${experiment_id}
    Assert Pdf As Expected    ${experiment_id}    ${expected_folder}\\${APP_SERVER_OS}\\${TEST NAME}.pdf    ${output_folder}\\${TEST NAME}.pdf

Check Get Published Record Fails Without CREATE_PDF Permission
    ${experiment_id}=    Create Publishable Experiment    GRP-S007    ${project_id}
    ${name}=    Set Variable    CUSTOM_GRP_S007
    ${entity_version_id}=    Get Entity Version ID    ${experiment_id}
    ${role_name}=    Create Custom PDF Role    ${name}
    ${permission_response}=    Remove Role Permission    ${role_name}    CREATE_PDF
    ${response}=    Assign User Entity Role    ${LIMITED_PERMISSIONS_USERNAME}    ${role_name}    ${experiment_id}
    Set Test Variable    ${SERVICES USERNAME}    ${LIMITED_PERMISSIONS_USERNAME}
    Set Test Variable    ${SERVICES PASSWORD}    ${PASSWORD}
    Log    As a new user...    INFO
    ${pdf_response}=    Export Record To PDF    ${experiment_id}    ${entity_version_id}    403
    Response Body Should Contain    Authorization failure: Need create PDF, PDF_FOLDER and either record OPEN or EDIT permissions
    ${unassign_role}=    Delete User Entity Role    ${LIMITED_PERMISSIONS_USERNAME}    ${role_name}    ${experiment_id}
    Run Keyword And Ignore Error    Delete Custom PDF Role    ${role_name}
