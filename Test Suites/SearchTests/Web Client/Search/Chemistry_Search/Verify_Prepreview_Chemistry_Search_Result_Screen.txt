*** Settings ***
Documentation     Verify Prepreview Chemistry Search Result Screen
...
...               created for SH-1001 jira task
Suite Setup       setup
Suite Teardown    teardown
Test Setup        Navigate Search Screen
Test Teardown     Go To EWB Home
Resource          ../../../../Resources/UI/Search_Page/chemistry_search_prepreview.txt
Resource          ../../../../Resources/UI/Search_Page/marvin_js.txt
Resource          ../../../../Resources/UI/Search_Page/search_ui.txt
Resource          ../../../../Resources/UI/Search_Page/search_result_ui.txt
Resource          ../../../../Resources/UI/navigator.txt
Library           DateTime
Library           IDBSSelenium2Library
Library           EntityAPILibrary
Library           OracleLibrary
Library           OperatingSystem

*** Test Cases ***
Verify Chemistry Prepreview Screen Text Labels
    [Documentation]    Verify Chemistry Prepreview Screen Text Labels
    ...    - execute chemistry search to have molecules and reactions as result
    ...    - verify all labels and links are in the screen
    ...
    ...    SH-498
    [Tags]    SRCH-CHEM    BROWSER_REGRESSION
    Select Search By Option    Chemistry
    Open MarvinJS From Search Screen
    Execute Chemistry Search By One Ring    Substructure search    Benzene
    Wait Until Page Contains Element    ${chemistrySearchUniqueMatchesLabel}
    Wait Until Page Contains Element    ${moleculesPreviewLink}
    Wait Until Page Contains Element    ${reactionsPreviewLink}
    Wait Until Page Contains Element    ${informationLabel}
    Wait Until Page Contains Element    ${doneChemistryButton}
    Wait Until Page Contains Element    ${prepreviewCancelButton}

Verify Search Result When No Item Selected In Preview Screen
    [Documentation]    Verify Search Result When No Item Selected In Preview Screen
    ...    - execute substructure chemistry search
    ...    - select nothing on prepreview screen, click Done
    ...    - verify all expected items are on search result list
    ...
    ...    SH-505
    [Tags]    SRCH-CHEM    BROWSER_REGRESSION
    Select Search By Option    Chemistry
    Open MarvinJS From Search Screen
    Execute Chemistry Search By One Ring    Substructure search    Benzene
    chemistry_search_prepreview.Click Done Button
    Verify Search Result List Contains Elements By Title    Template - Molecule and Reaction    Expt - Molecule

Verify Search Result When A Subset of Items Selected In Prepreview Screen
    [Documentation]    Verify Search Result When A Subset of Items Selected In Prepreview Screen
    ...    - the test is for verification, that the only items \ which are picked to be previewed are previewed
    ...    - execute chemistry search, select only record with reactions to be previewed
    ...    - verify result
    ...    - execute chemistry search, select only record with molecule to be previewed
    ...    - verify result
    ...
    ...    SH-505
    [Tags]    SRCH-CHEM    BROWSER_REGRESSION
    Select Search By Option    Chemistry
    Open MarvinJS From Search Screen
    Execute Chemistry Search By One Ring    Substructure search    Benzene
    Select Reaction Items To Be Previewed By Chemaxon ID    ${IodonaphthaleneReaction_ID}
    chemistry_search_prepreview.Click Done Button
    Verify Search Result List Contains Elements By Title    Template - Molecule and Reaction
    Verify Search Result List Does Not Contain Elements By Title    Expt - Molecule
    Open MarvinJS From Search Screen
    Click MarvinJS Search Button
    Select Molecule Items To Be Previewed By Chemaxon ID    ${Benzene_ID}
    chemistry_search_prepreview.Click Done Button
    Verify Search Result List Contains Elements By Title    Expt - Molecule
    Verify Search Result List Does Not Contain Elements By Title    Template - Molecule and Reaction

Verify Molecules And Reactions Count
    [Documentation]    Verify Molecules And Reactions Count
    ...    - since test data could not be unique, test could verify only that the number of molecules not less that expected to be.
    ...    - will test that number of molecules equal or greater than expected
    ...
    ...    SH-505
    [Tags]    SRCH-CHEM    BUILD_ACCEPTANCE    BROWSER_REGRESSION
    Select Search By Option    Chemistry
    Open MarvinJS From Search Screen
    Execute Chemistry Search By One Ring    Substructure search    Benzene
    Verify Prepreview Molecules Number Equals Or Greater    ${benzeneId}    1
    Verify Prepreview Molecules Number Equals Or Greater    ${methylnaphthaleneId}    2
    Verify Prepreview Reactions Number Equals Or Greater    ${iodonaphthaleneReactionId}    1

Verify Search by Multiple Discrete Molecules
    [Documentation]    Verify Search by Multiple Discrete Molecules
    ...    - Perform Search By 2 Benzene molecules
    ...    - Verify Methylnaphthalene present in search result
    ...    - Perform Search By 3 benzene molecules
    ...    - Verify Methylnaphthalene is not present in search result
    ...    - Check this conditions for reactions and benzene molecule
    [Tags]    SRCH-CHEM    ignore
    Select Search By Option    Chemistry
    Open MarvinJS From Search Screen
    Execute Chemistry Search By Non-connected Rings    Substructure search    Benzene|Benzene
    Verify Prepreview Molecules Search List Does Not Contain Elements    ${benzeneId}    ${methylnaphthaleneId}
    Verify Prepreview Reactions Search List Contains Elements    ${iodonaphthaleneReactionId}
    Click Prepreview Cancel Button
    Execute Chemistry Search By Non-connected Rings    Substructure search    Benzene|Benzene|CycloPentane
    Verify Blank Search Result
    Open MarvinJS From Search Screen
    Clear MarvinJS Canvas
    Execute Chemistry Search By Non-connected Rings And Elements    Substructure search    Benzene    I
    Verify Prepreview Molecules Search List Does Not Contain Elements    ${benzeneId}    ${methylnaphthaleneId}
    Verify Prepreview Reactions Search List Contains Elements    ${iodonaphthaleneReactionId}

*** Keywords ***
setup
    [Documentation]    Setup For 'Chemistry search' suite
    ...
    ...    - Create 'Chemistry Items' group
    ...    - Create \ 'Project 1' project with experiment 'Expt - Molecule' with 2 chemistry items inside
    ...    - Create 'Template - Molecule and Reaction' template from 'Project 1' \ with reaction and molecule inside
    ...    - it seturs suite variable for project ID, reaction \ and 3 molecules chemaxon IDs
    ${currentDate}=    Get Current Date
    ${methylnaphthaleneData}=    Get File    ${CURDIR}/Test Data/Methylnaphthalene (SH-520).txt
    ${benzeneData}=    Get File    ${CURDIR}/Test Data/Benzene.txt
    ${iodonaphthaleneReactionData}=    Get File    ${CURDIR}/Test Data/Iodonaphthalene reaction (SH-520).txt
    ${methylnaphthaleneDataTemplate}=    Get File    ${CURDIR}/Test Data/Methylnaphthalene (SH-520).txt
    ${groupName}=    Set Variable    Group ${currentDate}
    ${groupId}=    EntityAPILibrary.Ensure Group Is Present    1    ${groupName}
    Set suite Variable    ${groupId}
    ${projectId}=    EntityAPILibrary.Create Project    ${groupId}    Project 1
    ${experimentId}=    EntityAPILibrary.Create Experiment    ${projectId}    Expt - Molecule    Started
    ${methylnaphthaleneChem}=    EntityAPILibrary.Create Chemistry item    ${experimentId}
    EntityAPILibrary.Edit Chemistry Item    ${methylnaphthaleneChem}    ${experimentId}    ${methylnaphthaleneData}
    ${benzeneChem}=    EntityAPILibrary.Create Chemistry item    ${experimentId}
    EntityAPILibrary.Edit Chemistry Item    ${benzeneChem}    ${experimentId}    ${benzeneData}
    EntityAPILibrary.Version Save    ${experimentId}    Data Added
    ${templateId}=    EntityAPILibrary.Create Template    ${projectId}    Template - Molecule and Reaction    Planned
    ${iodonaphthaleneReactionChem}=    EntityAPILibrary.Create Chemistry item    ${templateId}
    EntityAPILibrary.Edit Chemistry Item    ${iodonaphthaleneReactionChem}    ${templateId}    ${iodonaphthaleneReactionData}
    ${methylnaphthaleneChemTemplate}=    EntityAPILibrary.Create Chemistry item    ${templateId}
    EntityAPILibrary.Edit Chemistry Item    ${methylnaphthaleneChemTemplate}    ${templateId}    ${methylnaphthaleneDataTemplate}
    EntityAPILibrary.Version Save    ${templateId}    Data Added
    Connect To Database    IDBS_EWB_CORE    IDBS_EWB_CORE    ${DB_SERVER}    ${ORACLE_SID}
    ${methylnaphthaleneIdList}=    Query    select distinct CHEMAXON_MOLECULE_ID from LATEST_ENTITY_VERSION_MOLECULE where ENTITY_ID in ('${methylnaphthaleneChemTemplate}', '${methylnaphthaleneChem}')
    ${iodonaphthaleneReactionIdList}=    Query    select distinct CHEMAXON_REACTION_ID from LATEST_ENTITY_VERSION_REACTION where ENTITY_ID in ('${iodonaphthaleneReactionChem}')
    ${benzeneIdList}=    Query    select distinct CHEMAXON_MOLECULE_ID from LATEST_ENTITY_VERSION_MOLECULE where ENTITY_ID in ('${benzeneChem}')
    ${methylnaphthaleneId}=    set variable    ${methylnaphthaleneIdList[0][0]}
    ${iodonaphthaleneReactionId}=    set variable    ${iodonaphthaleneReactionIdList[0][0]}
    ${benzeneId}=    set variable    ${benzeneIdList[0][0]}
    Set suite Variable    ${methylnaphthaleneId}
    Set suite Variable    ${iodonaphthaleneReactionId}
    Set suite Variable    ${benzeneId}
    Login to EWB

teardown
    [Documentation]    Teardown for 'Chemistry Search' suite
    EntityAPILibrary.Unlock Entity And Children    ${groupId}
    EntityAPILibrary.Delete Entity    ${groupId}    As intended    As intended
    close browser
