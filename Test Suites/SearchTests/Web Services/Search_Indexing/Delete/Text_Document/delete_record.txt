*** Settings ***
Documentation     Created for story SH-1296
Suite Setup       setup suite
Suite Teardown    teardown suite
Test Setup        setup test
Library           ../../../../../Resources/python/SearchAPILibrary/
Library           DateTime
Library           Collections
Resource          ../../../../../../Libraries/common_resource.txt
Resource          ../../../../../../Libraries/Web Client/Selenium/record_resource.txt
Library           EntityAPILibrary
Resource          ../../../../../Resources/API/Entity/entity_api.txt

*** Test Cases ***
Record - Add Document - Version - Delete Record
    [Documentation]    Record - Add Document - Version - Delete Record
    ...    - verify document is not searchble by document text \ after version record deleting
    ${experimentId}=    EntityAPILibrary.Create Experiment    ${projectId}    Experiment    Started
    ${documentId}=    EntityAPILibrary.Create Text Document    ${experimentId}    Document Name
    Edit Text Document And Version Save    ${experimentId}    ${documentId}    ${uniqueSearchText}
    EntityAPILibrary.Delete Entity    ${experimentId}    As Intended    As Intended
    Sleep    ${wait}
    Execute Free Text Search And Verify No Result    ${uniqueSearchText}

Record - Add Document - Draft - Delete Record
    [Documentation]    Record - Add Document - Draft - Delete Record
    ...    - verify document is not searchble by document text after draft record deleting
    ${experimentId}=    EntityAPILibrary.Create Experiment    ${projectId}    Experiment    Started
    ${documentId}=    EntityAPILibrary.Create Text Document    ${experimentId}    Document Name
    Edit Text Document And Draft Save    ${experimentId}    ${documentId}    ${uniqueSearchText}
    EntityAPILibrary.Delete Entity    ${experimentId}    As Intended    As Intended
    Sleep    ${wait}
    Execute Free Text Search And Verify No Result    ${uniqueSearchText}

*** Keywords ***
setup suite
    ${wait}=    Set Variable    15s
    Set Suite Variable    ${wait}
    ${currentDate}=    Get Current Date
    ${groupId}=    EntityAPILibrary.Ensure Group Is Present    1    Group ${currentDate}
    Set Suite Variable    ${groupId}
    ${projectId}=    EntityAPILibrary.Create Project    ${groupId}    project
    Set Suite Variable    ${projectId}

teardown suite
    EntityAPILibrary.Unlock Entity And Children    ${groupId}
    EntityAPILibrary.Delete Entity    ${groupId}    As intended    As intended

setup test
    ${experimentId}=    Set Variable    ${EMPTY}
    Set Suite Variable    ${experimentId}
    ${uniqueSearchText}=    Generate Random String    10    [NUMBERS]abcdef    # generates a 10 char "hex" string
    Set Suite Variable    ${uniqueSearchText}
